plugins {
    id 'com.gradle.build-scan' version '3.3.1'
    id 'nu.studer.plugindev' version '1.0.12'
    id 'nu.studer.credentials' version '2.1'
    id 'groovy'
}

group = 'nu.studer'
version = '4.3-DEV'

buildScan {
    if (project.properties['gradle-jooq-plugin.acceptGradleTOS']) {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
    publishAlways()
    obfuscation {
        ipAddresses { addresses -> addresses.collect { address -> '0.0.0.0' } }
    }
}

dependencies {
    implementation("org.jooq:jooq-codegen:3.13.1")

    implementation("javax.xml.bind:jaxb-api:2.3.1")
    implementation("javax.activation:activation:1.1.1")
    implementation("com.sun.xml.bind:jaxb-core:2.3.0.1")
    implementation("com.sun.xml.bind:jaxb-impl:2.3.0.1")

    testImplementation "com.h2database:h2:1.4.200"
    testImplementation("org.spockframework:spock-core:1.3-groovy-2.5") {
        exclude module: 'groovy-all'
    }
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.6.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

plugindev {
    pluginImplementationClass 'nu.studer.gradle.jooq.JooqPlugin'
    pluginDescription 'Gradle plugin that integrates jOOQ. For each configuration, the plugin adds a task to generate the sources from a given database schema and includes the generated sources in the specified source set. Multiple configurations are supported. The code generation tasks fully participate in the Gradle uptodate checks.'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'jooq'
    authorId 'etiennestuder'
    authorName 'Etienne Studer'
    authorEmail 'etienne@studer.nu'
    projectUrl 'https://github.com/etiennestuder/gradle-jooq-plugin'
    projectInceptionYear '2014'
    done()
}

bintray {
    user = credentials.BINTRAY_USER
    key = credentials.BINTRAY_API_KEY
    pkg.repo = 'gradle-plugins'
    dryRun = false
}

def testAll = tasks.create('testAll') {
    description = 'Runs the Gradle cross-version tests.'
    group = 'Verification'
}

List<String> testedGradleVersions = []
testedGradleVersions << "6.4.1"
testedGradleVersions << "6.0.1"
testedGradleVersions << "5.6.4"
testedGradleVersions << "5.0"

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}

tasks.withType(Test).configureEach {
    maxParallelForks = 2
    useJUnitPlatform() {
        includeEngines 'junit-vintage'
    }
}
