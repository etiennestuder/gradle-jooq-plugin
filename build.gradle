plugins {
    id 'org.gradle.kotlin.kotlin-dsl' version '1.2.1'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'com.gradle.build-scan' version '2.2.1'
    id 'nu.studer.plugindev' version '1.0.9'
    id 'nu.studer.credentials' version '1.0.5'
    id 'groovy'
}

group = 'nu.studer'
version = '3.0.4-DEV'

buildScan {
    termsOfServiceUrl 'https://gradle.com/terms-of-service'
    termsOfServiceAgree 'yes'
}

dependencies {
    compile("org.jooq:jooq-codegen:3.11.9")

    compile("javax.xml.bind:jaxb-api:2.3.1")
    compile("javax.activation:activation:1.1.1")
    compile("com.sun.xml.bind:jaxb-core:2.3.0.1")
    compile("com.sun.xml.bind:jaxb-impl:2.3.0.1")

    testCompile "com.h2database:h2:1.4.193"
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
        exclude module: 'groovy-all'
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

plugindev {
    pluginImplementationClass 'nu.studer.gradle.jooq.JooqPlugin'
    pluginDescription 'Gradle plugin that integrates jOOQ. For each configuration, the plugin adds a task to generate the sources from a given database schema and includes the generated sources in the specified source set. Multiple configurations are supported. The code generation tasks fully participate in the Gradle uptodate checks.'
    pluginLicenses 'Apache-2.0'
    pluginTags 'gradle', 'plugin', 'jooq'
    authorId 'etiennestuder'
    authorName 'Etienne Studer'
    authorEmail 'etienne@studer.nu'
    projectUrl 'https://github.com/etiennestuder/gradle-jooq-plugin'
    projectInceptionYear '2014'
    done()
}

bintray {
    user = credentials.BINTRAY_USER
    key = credentials.BINTRAY_API_KEY
    pkg.repo = 'gradle-plugins'
    dryRun = false
}

test {
    maxParallelForks = 2
}

def testAll = tasks.create('testAll') {
    description = 'Runs the Gradle cross-version tests.'
    group = 'Verification'
}

List<String> testedGradleVersions = []
testedGradleVersions << "5.1"
testedGradleVersions << "5.0"
testedGradleVersions << "4.10.3"
testedGradleVersions << "4.0.2"
testedGradleVersions << "3.5.1"

testedGradleVersions.each { version ->
    project.tasks.create("test_" + version.replaceAll("[^a-zA-Z0-9]", "_"), Test).with {
        systemProperty 'testContext.gradleVersion', version
        testAll.dependsOn(it)
    }
}
repositories {
    mavenCentral()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// Required in order to support building a mixed kotlin/groovy project. With out this,
// we would get a cyclic dependency error, since both compileKotlin and compileGroovy
// depend on compileJava.
// https://discuss.gradle.org/t/kotlin-groovy-and-java-compilation/14903/10
project.afterEvaluate {
    compileGroovy.dependsOn = compileGroovy.taskDependencies.mutableValues - 'compileJava'
    compileKotlin.dependsOn compileGroovy
    compileKotlin.classpath += files(compileGroovy.destinationDir)
    classes.dependsOn compileKotlin
}

// run tests with Java 9
//project.gradle.taskGraph.whenReady { taskGraph ->
//    project.tasks.withType(Test) { Test testTask ->
//        if (taskGraph.hasTask(testTask)) {
//            testTask.executable = new File('/Library/Java/JavaVirtualMachines/jdk-9.0.4.jdk/Contents/Home/bin/java')
//        }
//    }
//}
