import org.gradle.util.GradleVersion

import static java.lang.Integer.parseInt

plugins {
    id 'java-gradle-plugin'
    alias(libs.plugins.publish)
    alias(libs.plugins.github.actions)
    id 'groovy'
}

group = 'nu.studer'
version = '10.1.2-DEV'

configurations.configureEach { Configuration c ->
    c.resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'jakarta.xml.bind' && details.requested.name == 'jakarta.xml.bind-api') {
            details.useVersion '3.0.1'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api libs.bundles.jooq
    runtimeOnly libs.bundles.jaxb
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.BELLSOFT
    }
}

def dvBuildScan = develocity.buildScan
testing {
    suites {
        test {
            dependencies {
                implementation libs.h2
                implementation libs.spock
                implementation libs.groovy.xml
                implementation libs.groovy.sql
                runtimeOnly libs.junit5.launcher
            }
            targets {
                //noinspection ConfigurationAvoidance
                all {
                    testTask.configure { Test t ->
                        t.maxParallelForks = 1 // there is currently only a single test class
                        t.useJUnitPlatform()

                        String testJavaRuntimeVersion = findProperty('testJavaRuntimeVersion') ?: '25'
                        t.javaLauncher.set(javaToolchains.launcherFor { spec ->
                            spec.languageVersion.set(JavaLanguageVersion.of(testJavaRuntimeVersion))
                            dvBuildScan.value(t.identityPath.asString() + "#jvmVersion", testJavaRuntimeVersion)
                        } as Provider<? extends JavaLauncher>)

                        String testGradleVersion = findProperty('testGradleVersion') ?: GradleVersion.current().version
                        t.systemProperty 'testContext.gradleVersion', testGradleVersion
                        dvBuildScan.value(t.identityPath.asString() + "#gradleVersion", testGradleVersion)

                        def incompatibleJavaVsGradleVersions =
                                parseInt(testJavaRuntimeVersion) < 17 && GradleVersion.version(testGradleVersion) >= GradleVersion.version('9.0') ||
                                parseInt(testJavaRuntimeVersion) > 20 && GradleVersion.version(testGradleVersion) < GradleVersion.version('8.6') ||
                                parseInt(testJavaRuntimeVersion) > 16 && GradleVersion.version(testGradleVersion) < GradleVersion.version('7.3') ||
                                parseInt(testJavaRuntimeVersion) > 15 && GradleVersion.version(testGradleVersion) < GradleVersion.version('7.0')
                        if (incompatibleJavaVsGradleVersions) {
                            t.enabled = false
                        }
                    }
                }
            }
        }
    }
}

tasks.withType(AbstractCompile).configureEach {
    options.compilerArgs <<
            "-Werror" <<
            "-Xlint:all"
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.withType(ValidatePlugins.class).configureEach {
    failOnWarning = true
    enableStricterValidation = true
}

gradlePlugin {
    website = 'https://github.com/etiennestuder/gradle-jooq-plugin'
    vcsUrl = 'https://github.com/etiennestuder/gradle-jooq-plugin'
    plugins {
        pluginDevPlugin {
            id = 'nu.studer.jooq'
            displayName = 'gradle-jooq-plugin'
            description = 'Gradle plugin that integrates jOOQ.'
            tags.set(['jooq'])
            implementationClass = 'nu.studer.gradle.jooq.JooqPlugin'
        }
    }
}
