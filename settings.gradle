plugins {
    id 'com.gradle.develocity' version '3.19'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.0.2'
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

def isCI = System.getenv('CI') != null

develocity {
    server = 'https://etiennestuder.gradle-enterprise.cloud'

    buildScan {
        publishing.onlyIf { it.authenticated }
        uploadInBackground = !isCI
        capture { fileFingerprints = true }
        obfuscation { ipAddresses { addresses -> addresses.collect { address -> '0.0.0.0' } } }
    }
}

buildCache {
    local {
        enabled = true
    }
    remote(develocity.buildCache) {
        enabled = true
        // Check access key presence to avoid build cache errors on PR builds when access key is not present
        def accessKey = System.getenv("DEVELOCITY_ACCESS_KEY")
        push = isCI && accessKey
    }
}

rootProject.name = 'gradle-jooq-plugin'
