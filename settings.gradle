plugins {
    id 'com.gradle.enterprise' version '3.16.2'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.12.1'
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.7.0'
}

def isCI = System.getenv('CI') != null

gradleEnterprise {
    server = 'https://etiennestuder.gradle-enterprise.cloud'

    buildScan {
        publishAlways()
        publishIfAuthenticated()
        uploadInBackground = !isCI
        capture { taskInputFiles = true }
        obfuscation { ipAddresses { addresses -> addresses.collect { address -> '0.0.0.0' } } }
    }
}

buildCache {
    local {
        enabled = true
    }

    remote(gradleEnterprise.buildCache) {
        enabled = true
        // Check access key presence to avoid build cache errors on PR builds when access key is not present
        def accessKey = System.getenv("GRADLE_ENTERPRISE_ACCESS_KEY")
        push = isCI && accessKey
    }
}

rootProject.name = 'gradle-jooq-plugin'
