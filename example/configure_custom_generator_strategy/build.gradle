plugins {
    id 'nu.studer.jooq' version '7.1.1'
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    jooqGenerator 'com.h2database:h2:2.1.214'
    jooqGenerator project(':custom-generator-strategy')  // include sub-project that contains the custom generator strategy
}

jooq {
    version = '3.16.6'
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.h2.Driver'
                    url = 'jdbc:h2:~/test;AUTO_SERVER=TRUE'
                    user = 'sa'
                    password = ''
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    strategy {
                        // use the custom generator strategy
                        name = 'nu.studer.sample.SampleGeneratorStrategy'
                    }
                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                        includes = '.*'
                        excludes = ''
                    }
                    target {
                        packageName = 'nu.studer.sample'
                    }
                }
            }
        }
    }
}
