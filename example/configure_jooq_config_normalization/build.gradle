plugins {
    id 'nu.studer.jooq' version '7.1.1'
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    jooqGenerator 'com.h2database:h2:2.1.214'
}

jooq {
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.h2.Driver'
                    url = 'jdbc:h2:~/test;AUTO_SERVER=TRUE'
                    user = 'sa'
                    password = ''
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                        includes = '.*'
                        excludes = ''
                    }
                    target {
                        packageName = 'nu.studer.sample'
                    }
                }
            }
        }
    }
}

tasks.named('generateJooq').configure {
    // normalization is only meaningful when the task participates in incremental builds (and build caching)
    allInputsDeclared = true

    // normalize any parts of the jOOQ config to ensure relocatability (if task has been made cacheable)
    generationToolNormalization = { org.jooq.meta.jaxb.Configuration c ->
        c.jdbc.user = ''
        c.jdbc.password = ''
    }
}
