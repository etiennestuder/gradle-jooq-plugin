buildscript {
    configurations['classpath'].resolutionStrategy.eachDependency {
        if (requested.group == 'org.jooq') {
            // set jOOQ version used at configuration time to enforce a specific jOOQ config XML schema version
            useVersion '3.12.4'
        }
    }
}

plugins {
    id 'nu.studer.jooq' version '5.2.1'
    id 'java'
}

repositories {
    jcenter()
}

dependencies {
    jooqGenerator 'com.h2database:h2:1.4.200'
}

jooq {
    version = '3.12.4'

    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.h2.Driver'
                    url = 'jdbc:h2:~/test;AUTO_SERVER=TRUE'
                    user = 'sa'
                    password = ''
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                        includes = '.*'
                        excludes = ''
                    }
                    target {
                        packageName = 'nu.studer.sample'
                    }
                }
            }
        }
    }
}
