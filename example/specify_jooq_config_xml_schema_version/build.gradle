buildscript {
    configurations['classpath'].resolutionStrategy.eachDependency {
        if (requested.group.startsWith('org.jooq') && requested.name.startsWith('jooq')) {
            // set jOOQ version used at configuration time to enforce a specific jOOQ config XML schema version
            useVersion '3.20.10'
        }
    }
}

plugins {
    id 'nu.studer.jooq' version '10.1.1'
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    jooqGenerator 'com.h2database:h2:2.4.240'
}

jooq {
    version = '3.20.10' // set jOOQ version used at task execution time

    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.h2.Driver'
                    url = 'jdbc:h2:~/test;AUTO_SERVER=TRUE'
                    user = 'sa'
                    password = ''
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                        includes = '.*'
                        excludes = ''
                    }
                    target {
                        packageName = 'nu.studer.sample'
                    }
                }
            }
        }
    }
}
