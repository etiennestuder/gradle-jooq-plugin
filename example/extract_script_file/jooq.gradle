import nu.studer.gradle.jooq.JooqEdition

buildscript {
    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath 'nu.studer:gradle-jooq-plugin:6.0'
    }
}


apply plugin: nu.studer.gradle.jooq.JooqPlugin

repositories {
    mavenCentral()
}

dependencies {
    jooqGenerator 'com.h2database:h2:1.4.200'
}

jooq {
    version = '3.15.0'
    edition = JooqEdition.OSS

    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.h2.Driver'
                    url = 'jdbc:h2:~/test;AUTO_SERVER=TRUE'
                    user = 'sa'
                    password = ''
                    properties {
                        property {
                            key = 'PAGE_SIZE'
                            value = 2048
                        }
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.h2.H2Database'
                    }
                    generate {
                        deprecated = false
                        records = false
                        immutablePojos = false
                        fluentSetters = false
                    }
                    target {
                        packageName = 'nu.studer.sample'
                        directory = 'src/generated/jooq'
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}
